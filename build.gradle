apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


group = "com.allenru"
archivesBaseName = "cross-origin"
version = '1.0.1'


repositories {
    mavenCentral()
}

dependencies {
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'org.springframework', name: 'spring-core', version: '4.0.2.RELEASE'
    compile group: 'org.springframework', name: 'spring-web', version: '4.0.2.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '4.0.2.RELEASE'
}


jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Annotation based cross origin (CORS) support for Spring based servers.'
        url 'https://github.com/Russell-Allen/CrossOrigin'

        scm {  //scm:git:git://server_name[:port]/path_to_repository
          connection 'scm:git:git://github.com/Russell-Allen/CrossOrigin.git'
          developerConnection 'scm:git:git://github.com/Russell-Allen/CrossOrigin.git'
          url 'https://github.com/Russell-Allen/CrossOrigin.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            name 'Russell Allen'
            email 'allenru@gmail.com'
          }
        }
      }
    }
  }
}